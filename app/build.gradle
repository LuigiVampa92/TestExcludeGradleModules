plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'ru.tinkoff.gradle.jarjar'
}

android {
    compileSdk setup.compileSdk
    defaultConfig {
        applicationId "com.luigivampa92.excludemodules"
        minSdk setup.minSdk
        targetSdk setup.targetSdk
        versionCode appVersionCode
        versionName appVersionName
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation project(':core')
    implementation project(':app-di')
    implementation project(':testfeature-api')
    implementation project(':nestedfeature:api')
    implementation project(':standalonefeature')

    implementation fileTree(dir: './build/libs', include: ['*.jar'])     // добавляем в класспас путь к перепакованным либам
    implementation 'com.google.code.gson:gson:2.8.7'                     // директива на подключение реальной зависимости
    jarJar 'com.google.code.gson:gson:2.8.7'                             // директива на перепаковку зависимости по правилам джарджара

}

jarJar {
    jarJarDependency 'com.googlecode.jarjar:jarjar:1.3'                  // точное название артефакта в мавенцентрале (1.3 - последняя версия)
    rules = [                                                            // непосредственно сами правила
            'gson-2.8.7.jar': 'com.google.gson.** ru.tinkoff.core.gson.@1'
    ]
}